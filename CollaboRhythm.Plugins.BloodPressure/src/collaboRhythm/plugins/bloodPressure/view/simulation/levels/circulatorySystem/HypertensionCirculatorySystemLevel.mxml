<?xml version="1.0"?>
<!--~
  ~ Copyright 2011 John Moore, Scott Gilroy
  ~
  ~ This file is part of CollaboRhythm.
  ~
  ~ CollaboRhythm is free software: you can redistribute it and/or modify it under the terms of the GNU General Public
  ~ License as published by the Free Software Foundation, either version 2 of the License, or (at your option) any later
  ~ version.
  ~
  ~ CollaboRhythm is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied
  ~ warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
  ~ details.
  ~
  ~ You should have received a copy of the GNU General Public License along with CollaboRhythm.  If not, see
  ~ <http://www.gnu.org/licenses/>.
  -->
<simulation:SimulationLevelGroup
		xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:mx="library://ns.adobe.com/flex/mx"
		xmlns:simulation="collaboRhythm.plugins.bloodPressure.view.simulation.*"
		xmlns:gauge="collaboRhythm.plugins.bloodPressure.view.simulation.gauge.*"
		xmlns:circulatorysystem="collaboRhythm.plugins.bloodPressure.view.simulation.levels.circulatorySystem.*"
		xmlns:buttons="collaboRhythm.plugins.bloodPressure.view.simulation.buttons.*"
		width="100%" height="100%"
		title="Circulatory System"
		>
	<!--<s:SWFLoader id="cardiovascularSimulationLoader" source="@Embed(source='/assets/swfs/heart14.swf')" complete="circulatorySystemSimulationLoader_completeHandler()"/>-->
	<mx:UIComponent id="cardiovascularSimulationLoaderUiComponent" width="360" height="600"/>
	<s:Group id="arrowsGroup" width="100%" height="100%"/>
	<buttons:SimulationDetailButton
			id="atenololButton"
			x="{detailButtonX}"
			arrowTailY="{layoutDetailButtons(this.height, atenololButton.bottomExpansion, hydrochlorothiazideButton.bottomExpansion, details)}"
			expanded="false"
			listDataProvider="{createStepsData(atenololMedicationModel.steps, atenololMedicationModel.concentrationSeverityColor)}"
			label="Atenolol"
			subtitle="{atenololMedicationModel.drugClass}"
			move="drawArrows()"
			expand="hydrochlorothiazideButton.expanded = false"
			drillDownEnabled="false"
			>
		<buttons:leftContent>
			<simulation:MedicationsIcon left="7" verticalCenter="0" width="32" height="32"/>
		</buttons:leftContent>
		<buttons:rightContent>
			<simulation:HydrochlorothiazideGauge id="atenololGauge" verticalCenter="0"
												 width="60" height="30"
												 value="{atenololMedicationModel.concentration}"/>
		</buttons:rightContent>
	</buttons:SimulationDetailButton>
	<buttons:SimulationDetailButton
			id="hydrochlorothiazideButton"
			x="{detailButtonX}"
			expanded="true"
			listDataProvider="{createStepsData(hydrochlorothiazideMedicationModel.steps, hydrochlorothiazideMedicationModel.concentrationSeverityColor)}"
			label="Hydrochlorothiazide"
			subtitle="{hydrochlorothiazideMedicationModel.drugClass}"
			drillDownLevel="dispatchEvent(new SimulationLevelEvent(SimulationLevelEvent.DRILL_DOWN_LEVEL, 0.02, 0.02, hydrochlorothiazidePoint.x, hydrochlorothiazidePoint.y))"
			move="drawArrows()"
			expand="atenololButton.expanded = false"
			>
		<!--arrowTailY="{Math.max(hydrochlorothiazidePoint.y, atenololButton.arrowTailY + atenololButton.height + atenololButton.bottomExpansion + 20)}"-->
		<buttons:leftContent>
			<simulation:MedicationsIcon left="7" verticalCenter="0" width="32" height="32"/>
		</buttons:leftContent>
		<buttons:rightContent>
			<simulation:HydrochlorothiazideGauge id="hydrochlorothiazideGauge" verticalCenter="0"
												 width="60" height="30"
												 value="{hydrochlorothiazideMedicationModel.concentration}"/>
		</buttons:rightContent>
	</buttons:SimulationDetailButton>
	<fx:Script><![CDATA[
		import collaboRhythm.plugins.bloodPressure.view.simulation.*;
		import collaboRhythm.plugins.bloodPressure.view.simulation.buttons.SimulationDetail;
		import collaboRhythm.plugins.bloodPressure.view.simulation.buttons.StepListItem;
		import collaboRhythm.shared.apps.bloodPressure.model.BloodPressureModel;
		import collaboRhythm.shared.apps.bloodPressure.model.MedicationComponentAdherenceModel;
		import collaboRhythm.shared.apps.bloodPressure.model.SimulationModel;

		import mx.binding.utils.BindingUtils;

		import mx.collections.ArrayCollection;
		import mx.collections.IList;
		import mx.events.FlexEvent;
		import mx.events.PropertyChangeEvent;
		import mx.logging.ILogger;
		import mx.logging.Log;

		private const hydrochlorothiazidePoint:Point = new Point(200, 533);
		private const atenololPoint:Point = new Point(216, 342);
		[Bindable]
		private var hydrochlorothiazideMedicationModel:MedicationComponentAdherenceModel;
		[Bindable]
		private var atenololMedicationModel:MedicationComponentAdherenceModel;
		private var _circulatorySystemSimulationLoader:Loader = new Loader();
		private var _circulatorySystemSimulationMovieClip:MovieClip;
		protected var _logger:ILogger;

		protected override function creationCompleteHandler(event:FlexEvent):void
		{
			details = new <SimulationDetail>[
				new SimulationDetail(atenololPoint,
									 atenololButton),
				new SimulationDetail(hydrochlorothiazidePoint,
									 hydrochlorothiazideButton)
			];

			lookupMedications();

			cardiovascularSimulationLoaderUiComponent.addChild(_circulatorySystemSimulationLoader);
			_circulatorySystemSimulationLoader.contentLoaderInfo.addEventListener(Event.COMPLETE, circulatorySystemSimulationLoader_completeHandler);
			_circulatorySystemSimulationLoader.contentLoaderInfo.addEventListener(IOErrorEvent.IO_ERROR, circulatorySystemSimulationLoader_ioErrorHandler)
			var urlRequest:URLRequest = new URLRequest("http://www.mit.edu/~jom/temp/simulations/circulatorySystemSimulation.swf");
			_circulatorySystemSimulationLoader.load(urlRequest);

			_logger = Log.getLogger(getQualifiedClassName(this).replace("::", "."));

			super.creationCompleteHandler(event);
		}

		private function circulatorySystemSimulationLoader_completeHandler(event:Event):void
		{
			_circulatorySystemSimulationMovieClip = MovieClip(_circulatorySystemSimulationLoader.content);
		}

		private function circulatorySystemSimulationLoader_ioErrorHandler(event:Event):void
		{
			_logger.warn("circulatorySystemSimulator load FAILED");
		}

		private function hydrochlorothiazideMedicationModel_changeHandler(concentration:Number):void
		{
			if (_circulatorySystemSimulationMovieClip)
			{
				if (concentration < SimulationModel.HYDROCHLOROTHIAZIDE_GOAL)
				{
					_circulatorySystemSimulationMovieClip.current_preload = 3;
					_circulatorySystemSimulationMovieClip.current_damage = 2;
					_circulatorySystemSimulationMovieClip.update();
				}
				else
				{
					_circulatorySystemSimulationMovieClip.current_preload = 1;
					_circulatorySystemSimulationMovieClip.current_damage = 0;
					_circulatorySystemSimulationMovieClip.update();
				}
			}
		}

		private function createStepsData(steps:Vector.<String>, concentrationSeverityColor:uint):IList
		{
			var result:ArrayCollection = new ArrayCollection();

			var i:int;
			for each (var step:String in steps)
			{
				i++;
				result.addItem(new StepListItem(step, concentrationSeverityColor));
			}

			return result;
		}

		private function simulationModel_propertyChangeHandler(event:PropertyChangeEvent):void
		{
			if (event.property == "medications")
				lookupMedications();
		}

		private function lookupMedications():void
		{
			if (simulationModel)
			{
				hydrochlorothiazideMedicationModel = simulationModel.getMedication(BloodPressureModel.RXNORM_HYDROCHLOROTHIAZIDE);
				BindingUtils.bindSetter(hydrochlorothiazideMedicationModel_changeHandler,
										hydrochlorothiazideMedicationModel, "concentration");
				atenololMedicationModel = simulationModel.getMedication(BloodPressureModel.RXNORM_ATENOLOL);
			}
		}

		public override function set simulationModel(value:SimulationModel):void
		{
			super.simulationModel = value;
			if (simulationModel)
				simulationModel.addEventListener(PropertyChangeEvent.PROPERTY_CHANGE,
												 simulationModel_propertyChangeHandler);
			lookupMedications();
		}
		]]></fx:Script>
</simulation:SimulationLevelGroup>
