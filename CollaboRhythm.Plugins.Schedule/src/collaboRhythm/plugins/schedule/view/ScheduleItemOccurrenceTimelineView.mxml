<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 width="{SCHEDULE_ITEM_TIMELINE_VIEW_WIDTH}"
		 height="{SCHEDULE_ITEM_TIMELINE_VIEW_HEIGHT}"
		 remove="removeHandler()"
		 mouseDown="mouseDownHandler(event)">
	<fx:Declarations>
		<s:Fade id="hideEffect" alphaFrom="{this.alpha}" alphaTo="0" duration="0.01"/>
		<s:Animate id="stackEffect" target="{this}">
			<s:SimpleMotionPath id="motionPath" property="bottom"/>
		</s:Animate>
			<s:Callout id="scheduleItemCallout"
					horizontalPosition="after"
					verticalPosition="middle" mouseDownOutside="scheduleItemCallout_mouseDownOutsideHandler()">
				<s:VGroup gap="0">
					<s:Button label="Edit" width="150" height="60" click="editButton_clickHandler(event)"/>
					<s:Button label="Unschedule" width="150" height="60" click="unscheduleButton_clickHandler(event)"/>
				</s:VGroup>
			</s:Callout>
	</fx:Declarations>
	<fx:Style>

		#nameLabelTimeline {
			font-size: 18;
			font-weight: bold;
		}

		#descriptionLabelTimeline {
			font-size: 12;
		}

		#indicationLabelTimeline {
			fontSize: 12;
			fontStyle: italic;
		}

		#instructionsLabelTimeline {
			fontSize: 14;
			font-weight: bold;
		}
	</fx:Style>
	<fx:Script>
        <![CDATA[
		import collaboRhythm.plugins.schedule.model.ScheduleModel;
		import collaboRhythm.plugins.schedule.shared.model.IHealthActionListViewAdapter;
		import collaboRhythm.plugins.schedule.shared.model.MoveData;
		import collaboRhythm.plugins.schedule.shared.view.ScheduleTimelineViewEvent;
		import collaboRhythm.shared.model.healthRecord.document.MedicationScheduleItem;
		import collaboRhythm.shared.model.healthRecord.document.ScheduleItemOccurrence;

		import mx.binding.utils.ChangeWatcher;
		import mx.core.IVisualElement;

		import spark.components.Image;

		public static const SCHEDULE_ITEM_TIMELINE_VIEW_PICTURE_WIDTH:Number = 80;
		public static const SCHEDULE_ITEM_TIMELINE_VIEW_WIDTH:Number = 320;
		public static const SCHEDULE_ITEM_TIMELINE_VIEW_HEIGHT:Number = 82;

		[Bindable]
		private var _scheduleItemOccurrence:ScheduleItemOccurrence;
		private var _changeWatchers:Vector.<ChangeWatcher> = new Vector.<ChangeWatcher>();

		public function init(scheduleItemOccurrence:ScheduleItemOccurrence, scheduleModel:ScheduleModel):void
		{
			_scheduleItemOccurrence = scheduleItemOccurrence;

			var scheduleItemOccurrenceReportingViewAdapter:IHealthActionListViewAdapter = scheduleModel.healthActionListViewAdapterFactory.createScheduledHealthActionViewAdapter(_scheduleItemOccurrence,
					scheduleModel);

			nameLabelTimeline.text = scheduleItemOccurrenceReportingViewAdapter.name;
			descriptionLabelTimeline.text = scheduleItemOccurrenceReportingViewAdapter.description;
			indicationLabelTimeline.text = scheduleItemOccurrenceReportingViewAdapter.indication;
			instructionsLabelTimeline.text = scheduleItemOccurrenceReportingViewAdapter.secondaryInstructions;

			var customView:IVisualElement = scheduleItemOccurrenceReportingViewAdapter.createCustomView();
			if (customView)
			{
				imageRegion.addElement(customView);
			}
			else
			{
				var image:Image = scheduleItemOccurrenceReportingViewAdapter.createImage();
				image.percentWidth = 100;
				image.percentHeight = 100;
				image.smooth = true;
				image.addEventListener(MouseEvent.MOUSE_DOWN, image_clickHandler);
				imageRegion.addElement(image);
			}
			imageRegion.left = 5;
			imageRegion.right = 5;
			imageRegion.top = 5;
			imageRegion.bottom = 5;
		}

		private function image_clickHandler(event:MouseEvent):void
		{
			if (_scheduleItemOccurrence.scheduleItem as MedicationScheduleItem)
			{
				event.stopImmediatePropagation();

				scheduleItemCallout.open(event.target as DisplayObjectContainer, true);
			}
		}

		private function mouseDownHandler(event:MouseEvent):void
		{
			event.stopImmediatePropagation();

			if (!stackEffect.isPlaying)
			{
				var moveData:MoveData = createMoveData(event);
				dispatchEvent(new ScheduleTimelineViewEvent(ScheduleTimelineViewEvent.SCHEDULE_ITEM_TIMELINE_VIEW_MOUSE_DOWN,
						moveData));
			}
		}

		private function createMoveData(event:MouseEvent):MoveData
		{
			var moveData:MoveData = new MoveData();
			moveData.id = _scheduleItemOccurrence.id;
			moveData.localX = event.localX;
			moveData.localY = event.localY;
			moveData.stageX = event.stageX;
			moveData.stageY = event.stageY;

			return moveData;
		}

		private function movingChangeHandler(change:Boolean):void
		{
			if (change)
			{
				glowFilter.blurX = 10;
				glowFilter.blurY = 10;
			}
			else
			{
				glowFilter.blurX = 5;
				glowFilter.blurY = 5;
			}
		}

		public function destroy():void
		{
			hideEffect.end();
			stackEffect.end();
		}

		private function removeHandler():void
		{
			for each (var changeWatcher:ChangeWatcher in _changeWatchers)
			{
				changeWatcher.unwatch();
			}
		}

		public function get scheduleItemOccurrence():ScheduleItemOccurrence
		{
			return _scheduleItemOccurrence;
		}

		public function set scheduleItemOccurrence(value:ScheduleItemOccurrence):void
		{
			_scheduleItemOccurrence = value;
		}

		private function editButton_clickHandler(event:MouseEvent):void
		{
			scheduleItemCallout.close();

			var moveData:MoveData = createMoveData(event);
			dispatchEvent(new ScheduleTimelineViewEvent(ScheduleTimelineViewEvent.SCHEDULE_ITEM_TIMELINE_VIEW_EDIT,
					moveData));
		}

		private function unscheduleButton_clickHandler(event:MouseEvent):void
		{
			scheduleItemCallout.close();

			var moveData:MoveData = createMoveData(event);
			dispatchEvent(new ScheduleTimelineViewEvent(ScheduleTimelineViewEvent.SCHEDULE_ITEM_TIMELINE_VIEW_UNSCHEDULE,
					moveData));
		}

		private function scheduleItemCallout_mouseDownOutsideHandler():void
		{
			scheduleItemCallout.close();
		}
		]]>
    </fx:Script>

	<s:Rect id="background" width="100%" height="100%">
		<s:fill>
			<!--TODO: Reimplement coloring during collaboration-->
			<s:SolidColor color="0xFFFFFF" alpha="0.4"/>
		</s:fill>
		<s:stroke>
			<s:SolidColorStroke color="0xFFFFFF" weight="1" alpha="0.5"/>
		</s:stroke>
	</s:Rect>

	<s:Group id="imageContainerGroup" width="{this.height}" height="100%">
		<s:Group id="imageRegion"/>
	</s:Group>


	<s:Group id="descriptionRegion" height="100%" left="{imageContainerGroup.width}" right="{gripRegion.width}">
		<s:VGroup left="5" right="5" top="5" bottom="5" gap="1">
			<s:Label id="nameLabelTimeline" maxDisplayedLines="1" width="100%"/>
			<s:Label id="descriptionLabelTimeline" maxDisplayedLines="1" width="100%"/>
			<s:Label id="indicationLabelTimeline" paddingTop="5" maxDisplayedLines="1" width="100%"/>
			<s:Label id="instructionsLabelTimeline" paddingTop="5" maxDisplayedLines="1" width="100%"/>
		</s:VGroup>
	</s:Group>

	<s:Group id="gripRegion" right="5" height="100%">
		<s:Image verticalCenter="0" source="@Embed(source='/assets/images/gripHorizontalNarrow.png')"
				 skinClass="spark.skins.spark.ImageSkin"/>
	</s:Group>

	<s:filters>
		<s:GlowFilter id="glowFilter"
					  quality="3"
					  alpha="0.2"
					  color="0x000000"
					  blurX="5"
					  blurY="5"
					  strength="5"/>
	</s:filters>
</s:Group>
