<?xml version="1.0"?>
<!---
 - Copyright 2012 John Moore, Scott Gilroy
 -
 - This file is part of CollaboRhythm.
 -
 - CollaboRhythm is free software: you can redistribute it and/or modify it under the terms of the GNU General Public
 - License as published by the Free Software Foundation, either version 2 of the License, or (at your option) any later
 - version.
 -
 - CollaboRhythm is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied
 - warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 - details.
 -
 - You should have received a copy of the GNU General Public License along with CollaboRhythm.  If not, see
 - <http://www.gnu.org/licenses/>.
-->
<tablet:TabletViewBase xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:tablet="collaboRhythm.shared.view.tablet.*"
					   xmlns:view="collaboRhythm.plugins.foraD40b.view.*"
					   xmlns:sharedView="collaboRhythm.plugins.schedule.shared.view.*"
					   creationComplete="creationCompleteHandler(event)"
					   addedToStage="addedToStageHandler()"
					   implements="collaboRhythm.plugins.schedule.shared.model.IHealthActionInputView"
					   title="Hypoglycemia Action Plan - Step 1: Eat Quick Sugar">
	<fx:Declarations>
		<s:Button id="startActionButton" label="Start" click="startButton_clickHandler()"/>
	</fx:Declarations>
	<fx:Style>
		#interpretationLabel {
			fontSize: 36;
		}

		#startActionPlanLabel {
			fontSize: 48;
		}
	</fx:Style>
	<fx:Script><![CDATA[
		import collaboRhythm.plugins.foraD40b.controller.BloodGlucoseHealthActionInputController;
		import collaboRhythm.plugins.foraD40b.model.BloodGlucoseHealthActionInputModel;
		import collaboRhythm.plugins.schedule.shared.model.HealthActionInputModelAndController;
		import collaboRhythm.plugins.schedule.shared.model.IHealthActionInputController;

		import mx.events.FlexEvent;

		private var _dataInputController:BloodGlucoseHealthActionInputController;
		[Bindable]
		private var _dataInputModel:BloodGlucoseHealthActionInputModel;

		override public function set data(value:Object):void
		{
			super.data = value;

			var dataInputModelAndController:HealthActionInputModelAndController = HealthActionInputModelAndController(value);

			_dataInputModel = BloodGlucoseHealthActionInputModel(dataInputModelAndController.healthActionInputModel);
			_dataInputController = BloodGlucoseHealthActionInputController(dataInputModelAndController.healthActionInputController);
		}

		private function creationCompleteHandler(event:FlexEvent):void
		{
			hypoglycemiaActionPlanView.init(_dataInputModel);

			if (_dataInputModel.glycemicState == BloodGlucoseHealthActionInputModel.HYPOGLYCEMIA)
			{
				if (_dataInputModel.repeatCount == 0)
				{
					interpretationLabel.text = "Your blood glucose is low (< " +
							BloodGlucoseHealthActionInputModel.HYPOGLYCEMIA_THRESHOLD + " mg/dL).";
					showStartActionPlanMessage();
				}
				else
				{
					interpretationLabel.text = "Your blood glucose is still low (< " +
							BloodGlucoseHealthActionInputModel.REPEAT_HYPOGLYCEMIA_THRESHOLD + " mg/dL).";
					showContinueActionPlanMessage();
				}
			}
			else if (_dataInputModel.glycemicState == BloodGlucoseHealthActionInputModel.SEVERE_HYPOGLYCEMIA)
			{
				if (_dataInputModel.repeatCount == 0)
				{
					interpretationLabel.text = "Your blood glucose is very low (< " +
							BloodGlucoseHealthActionInputModel.SEVERE_HYPOGLYCEMIA_THRESHOLD + " mg/dL).";
					showStartActionPlanMessage();
				}
				else
				{
					interpretationLabel.text = "Your blood glucose is still very low (< " +
							BloodGlucoseHealthActionInputModel.SEVERE_HYPOGLYCEMIA_THRESHOLD + " mg/dL).";
					showContinueActionPlanMessage();
				}
			}
		}

		private function showStartActionPlanMessage():void
		{
			startActionPlanLabel.text = "You should start your hypoglycemia action plan.";
		}

		private function showContinueActionPlanMessage():void
		{
			startActionPlanLabel.text = "You should continue your hypoglycemia action plan.";
			startActionButton.label = "Continue";
			startButton.label = "Continue;"
		}

		override protected function createChildren():void
		{
			super.createChildren();
			actionContent.unshift(startActionButton);
		}

		private function addedToStageHandler():void
		{
			addEventListener(KeyboardEvent.KEY_UP, hardwareKeyUpHandler)
		}

		private function hardwareKeyUpHandler(event:KeyboardEvent):void
		{
			if (event.keyCode == Keyboard.BACK || event.keyCode == Keyboard.HOME)
			{
				event.stopImmediatePropagation();
				event.preventDefault();
			}
		}

		override public function homeButton_clickHandler():void
		{

		}

		public function get healthActionInputController():IHealthActionInputController
		{
			return _dataInputController;
		}

		private function startButton_clickHandler():void
		{
			_dataInputController.nextStep();
		}
		]]></fx:Script>
	<s:Group width="100%" height="100%">
		<view:HypoglycemiaActionPlanStepsView id="hypoglycemiaActionPlanView"/>
		<s:Label id="interpretationLabel" top="40" horizontalCenter="0"/>
		<s:Label id="startActionPlanLabel" horizontalCenter="0" verticalCenter="0"/>
		<s:Button id="startButton" label="Start" width="160" height="50" bottom="90" horizontalCenter="0"
				  click="startButton_clickHandler()"/>
	</s:Group>
</tablet:TabletViewBase>
