<?xml version="1.0"?>
<!---
 - Copyright 2012 John Moore, Scott Gilroy
 -
 - This file is part of CollaboRhythm.
 -
 - CollaboRhythm is free software: you can redistribute it and/or modify it under the terms of the GNU General Public
 - License as published by the Free Software Foundation, either version 2 of the License, or (at your option) any later
 - version.
 -
 - CollaboRhythm is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied
 - warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 - details.
 -
 - You should have received a copy of the GNU General Public License along with CollaboRhythm.  If not, see
 - <http://www.gnu.org/licenses/>.
-->
<tablet:TabletViewBase xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:view="collaboRhythm.plugins.foraD40b.view.*"
					   xmlns:tablet="collaboRhythm.shared.view.tablet.*"
					   implements="collaboRhythm.plugins.schedule.shared.model.IHealthActionInputView"
					   creationComplete="creationCompleteHandler(event)"
					   addedToStage="addedToStageHandler()">
	<fx:Declarations>
		<s:Button id="saveButton" label="Save" enabled="false" click="saveButton_clickHandler()"/>
	</fx:Declarations>
	<fx:Style>
		@namespace s "library://ns.adobe.com/flex/spark";

		.reportingLabelsText {
			fontSize: 40;
		}

		#recheckLabel {
			fontSize: 36;
		}

		.textInputText {
			fontSize: 110;
		}
	</fx:Style>
	<fx:Script><![CDATA[
		import collaboRhythm.plugins.foraD40b.controller.BloodGlucoseHealthActionInputController;
		import collaboRhythm.plugins.foraD40b.model.BloodGlucoseHealthActionInputModel;
		import collaboRhythm.plugins.schedule.shared.model.HealthActionInputModelAndController;
		import collaboRhythm.plugins.schedule.shared.model.IHealthActionInputController;

		import mx.binding.utils.BindingUtils;
		import mx.events.FlexEvent;

		import spark.skins.mobile.TextInputSkin;

		private const NUMBER_PAD_GAP:int = 2;

		[Bindable]
		protected var _dataInputModel:BloodGlucoseHealthActionInputModel;
		protected var _dataInputController:BloodGlucoseHealthActionInputController;

		override public function set data(value:Object):void
		{
			super.data = value;

			var dataInputModelAndController:HealthActionInputModelAndController = value as
					HealthActionInputModelAndController;

			_dataInputModel = BloodGlucoseHealthActionInputModel(dataInputModelAndController.healthActionInputModel);
			_dataInputController = BloodGlucoseHealthActionInputController(dataInputModelAndController.healthActionInputController);

			if (_dataInputModel.repeatCount == 0)
			{
				title = "Report Blood Glucose"
			}
			else
			{
				title = "Hypoglycemia Action Plan - Step 3: Recheck Blood Glucose"
			}

			BindingUtils.bindSetter(setDeviceBloodGlucose, _dataInputModel, "deviceBloodGlucose");
			BindingUtils.bindSetter(handleInvalidBloodGlucose, _dataInputModel, "invalidBloodGlucose");
		}

		override protected function createChildren():void
		{
			super.createChildren();
			if (_dataInputModel.repeatCount != 0)
			{
				saveButton.label = "Next Step";
				nextStepButton.visible = true;
			}
			actionContent.unshift(saveButton);
		}

		private function creationCompleteHandler(event:FlexEvent):void
		{
			if (_dataInputModel.repeatCount > 0)
			{
				hypoglycemiaActionPlanView.init(_dataInputModel);
				hypoglycemiaActionPlanView.visible = true;
				nextStepButton.visible = true;
			}

			if (_dataInputModel.deviceBloodGlucose != "")
			{
				showDeviceBloodGlucose();
			}
		}

		private function setDeviceBloodGlucose(deviceBloodGlucose:String):void
		{
			if (deviceBloodGlucose != "" && bloodGlucoseTextInput)
			{
				showDeviceBloodGlucose();
			}
		}

		private function handleInvalidBloodGlucose(invalidBloodGlucose:Boolean):void
		{
			if (invalidBloodGlucose)
			{
				//TODO: Decide whether to show an alert or flash the text box
			}
		}

		private function showDeviceBloodGlucose():void
		{
			wirelessIcon.visible = true;
			bloodGlucoseTextInput.setStyle("skinClass", TextInputSkin);
			bloodGlucoseTextInput.text = _dataInputModel.deviceBloodGlucose;
			bloodGlucoseTextInput.enabled = false;
		}

		public function get healthActionInputController():IHealthActionInputController
		{
			return _dataInputController;
		}

		private function addedToStageHandler():void
		{
			addEventListener(KeyboardEvent.KEY_UP, hardwareKeyUpHandler)
		}

		private function hardwareKeyUpHandler(event:KeyboardEvent):void
		{
			if (_dataInputModel.repeatCount > 0 && (event.keyCode == Keyboard.BACK || event.keyCode == Keyboard.HOME))
			{
				event.stopImmediatePropagation();
				event.preventDefault();
			}
		}

		override public function homeButton_clickHandler():void
		{
			if (_dataInputModel.repeatCount == 0)
			{
				super.homeButton_clickHandler();
			}
		}

		private function saveButton_clickHandler():void
		{
			_dataInputController.submitBloodGlucose(bloodGlucoseTextInput.text);
		}

		private function bloodGlucoseTextInput_changeHandler():void
		{
			if (bloodGlucoseTextInput.text == "")
			{
				saveButton.enabled = false;
				if (nextStepButton.visible)
				{
					nextStepButton.enabled = false;
				}
			}
			else
			{
				saveButton.enabled = true;
				if (nextStepButton.visible)
				{
					nextStepButton.enabled = true;
				}
			}
		}
		]]></fx:Script>
	<view:HypoglycemiaActionPlanStepsView id="hypoglycemiaActionPlanView" visible="false"/>
	<s:Group height="100%" width="100%">
		<s:HGroup id="bloodGlucoseGroup" gap="15" verticalAlign="middle" horizontalCenter="0" top="200">
			<s:Image source="http://www.mit.edu/~jom/temp/equipment/FORA D40b.png" width="140" height="140"
					 smooth="true"/>
			<view:WirelessIcon id="wirelessIcon" visible="false"/>
			<s:Group width="240" height="130">
				<s:TextInput id="bloodGlucoseTextInput" width="100%" height="100%"
							 skinClass="spark.skins.mobile.StageTextInputSkin" styleName="textInputText"
							 textAlign="right" restrict="0-9" maxChars="3" softKeyboardType="number"
							 change="bloodGlucoseTextInput_changeHandler()"/>
			</s:Group>
			<s:Label text="mg/dL" verticalCenter="0" styleName="reportingLabelsText"/>
		</s:HGroup>
		<s:Button id="nextStepButton" label="Next Step" width="160" height="50" bottom="90" horizontalCenter="0"
				  visible="false" enabled="false" click="saveButton_clickHandler()"/>
	</s:Group>
</tablet:TabletViewBase>