<?xml version="1.0"?>
<!---
 - Copyright 2012 John Moore, Scott Gilroy
 -
 - This file is part of CollaboRhythm.
 -
 - CollaboRhythm is free software: you can redistribute it and/or modify it under the terms of the GNU General Public
 - License as published by the Free Software Foundation, either version 2 of the License, or (at your option) any later
 - version.
 -
 - CollaboRhythm is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied
 - warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 - details.
 -
 - You should have received a copy of the GNU General Public License along with CollaboRhythm.  If not, see
 - <http://www.gnu.org/licenses/>.
-->
<tablet:TabletViewBase xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:view="collaboRhythm.plugins.foraD40b.view.*"
					   xmlns:tablet="collaboRhythm.shared.view.tablet.*"
					   implements="collaboRhythm.plugins.schedule.shared.model.IHealthActionInputView"
					   creationComplete="creationCompleteHandler(event)">
	<fx:Declarations>
		<s:Button id="saveActionButton" label="Save" enabled="false" click="saveButton_clickHandler()"/>
		<s:DateTimeFormatter id="timeFormatter" dateTimePattern="h a"/>
	</fx:Declarations>
	<fx:Script><![CDATA[
		import collaboRhythm.plugins.foraD40b.controller.BloodGlucoseHealthActionInputController;
		import collaboRhythm.plugins.foraD40b.model.BloodGlucoseHealthActionInputModel;
		import collaboRhythm.plugins.foraD40b.model.BloodGlucoseHealthActionInputModelCollection;
		import collaboRhythm.plugins.foraD40b.model.ReportBloodGlucoseItemData;
		import collaboRhythm.plugins.schedule.shared.model.HealthActionInputModelAndController;
		import collaboRhythm.plugins.schedule.shared.model.IHealthActionInputController;
		import collaboRhythm.shared.model.DateUtil;
		import collaboRhythm.shared.model.healthRecord.document.VitalSign;
		import collaboRhythm.shared.model.services.ICurrentDateSource;
		import collaboRhythm.shared.model.services.WorkstationKernel;

		import mx.binding.utils.BindingUtils;
		import mx.events.FlexEvent;

		import spark.components.ViewMenuItem;

		[Bindable]
		protected var _dataInputModel:BloodGlucoseHealthActionInputModelCollection;
		protected var _dataInputController:BloodGlucoseHealthActionInputController;
		private var _currentDateSource:ICurrentDateSource;

		override public function set data(value:Object):void
		{
			super.data = value;

			var dataInputModelAndController:HealthActionInputModelAndController = value as
					HealthActionInputModelAndController;

			_dataInputModel = dataInputModelAndController.healthActionInputModel as
					BloodGlucoseHealthActionInputModelCollection;
			_dataInputController = dataInputModelAndController.healthActionInputController as
					BloodGlucoseHealthActionInputController;

			_currentDateSource = WorkstationKernel.instance.resolve(ICurrentDateSource) as ICurrentDateSource;
		}

		override protected function createChildren():void
		{
			super.createChildren();
			if (_dataInputModel.scheduleItemOccurrence && _dataInputModel.scheduleItemOccurrence.adherenceItem)
			{
				title = "Review Blood Glucose Measurement - Scheduled (" +
						timeFormatter.format(_dataInputModel.scheduleItemOccurrence.dateStart) + " to " +
						timeFormatter.format(_dataInputModel.scheduleItemOccurrence.dateEnd) + ")";
				saveButton.visible = false;
			}
			else if (_dataInputModel.scheduleItemOccurrence)
			{
				title = "Report Blood Glucose Measurement - Scheduled (" +
						timeFormatter.format(_dataInputModel.scheduleItemOccurrence.dateStart) + " to " +
						timeFormatter.format(_dataInputModel.scheduleItemOccurrence.dateEnd) + ")";
				actionContent.unshift(saveActionButton);
			}
			else
			{
				title = "Report Blood Glucose Measurement - Unscheduled";
				actionContent.unshift(saveActionButton);
			}
			addMenuItem("Simulate Device", simulateDeviceMenuItem_clickHandler);
			addMenuItem("Simulate Device 2", simulateDevice2MenuItem_clickHandler);
		}

		private function creationCompleteHandler(event:FlexEvent):void
		{
			reportBloodGlucoseDetailedView.init(_dataInputModel, _dataInputController);

			saveActionButton.enabled = false;
			saveButton.enabled = false;

			BindingUtils.bindSetter(setManualBloodGlucose, _dataInputModel.firstInputModel, "manualBloodGlucose");
			BindingUtils.bindSetter(setDeviceBloodGlucose, _dataInputModel.firstInputModel, "deviceBloodGlucose");
		}

		private function setManualBloodGlucose(manualBloodGlucose:String):void
		{
			if (manualBloodGlucose == "")
			{
				saveActionButton.enabled = false;
				saveButton.enabled = false;
			}
			else if (manualBloodGlucose != "" &&
					_dataInputModel.healthActionModelDetailsProvider.accountId ==
							_dataInputModel.healthActionModelDetailsProvider.activeAccount.accountId)
			{
				saveActionButton.enabled = true;
				saveButton.enabled = true;
			}
		}

		private function setDeviceBloodGlucose(deviceBloodGlucose:String):void
		{
			if (deviceBloodGlucose != "" &&
					_dataInputModel.healthActionModelDetailsProvider.accountId ==
							_dataInputModel.healthActionModelDetailsProvider.activeAccount.accountId)
			{
				saveActionButton.enabled = true;
				saveButton.enabled = true;
			}
		}

		public function get healthActionInputController():IHealthActionInputController
		{
			return _dataInputController;
		}

		private function saveButton_clickHandler():void
		{
			_dataInputController.createAndSubmitBloodGlucoseVitalSign();
		}

		override public function homeButton_clickHandler():void
		{
			_dataInputController.removeCollaborationViewSynchronizationEventListener();

			super.homeButton_clickHandler();
		}

		override protected function stage_keyUpHandler(event:KeyboardEvent):void
		{
			if (event.keyCode == Keyboard.BACK)
			{
				_dataInputController.removeCollaborationViewSynchronizationEventListener();
			}

			super.stage_keyUpHandler(event);
		}

		private function simulateDeviceMenuItem_clickHandler(event:MouseEvent):void
		{
			var index:Number;
			if ((_dataInputModel.reportBloodGlucoseItemDataCollection[0] as
					ReportBloodGlucoseItemData).dataInputModel.isFromDevice)
			{
				index = _dataInputModel.reportBloodGlucoseItemDataCollection.length;
			}
			else
			{
				index = 0;
			}
			var numToSimulate:Number = 3;

			simulateDeviceBatchTransferBegin();
			for (var i:int = 0; i < numToSimulate; i++)
			{
				simulateDeviceOffsetFromNow(index / numToSimulate + i * (1.0 / numToSimulate), 1.0 / numToSimulate);
			}
			simulateDeviceBatchTransferEnd();
		}

		private function simulateDevice2MenuItem_clickHandler(event:MouseEvent):void
		{
			var dateValue:Number;
			if (_dataInputModel && _dataInputModel.scheduleItemOccurrence)
			{
				dateValue = _dataInputModel.scheduleItemOccurrence.dateEnd.valueOf();
			}

			if (_dataInputModel && _dataInputModel.lastInputModel &&
					_dataInputModel.lastInputModel.dateMeasuredStart.valueOf() < dateValue)
			{
				dateValue = _dataInputModel.lastInputModel.dateMeasuredStart.valueOf();
			}

			if (!isNaN(dateValue))
			{
				simulateDeviceBatchTransferBegin();
				simulateDevice(150, new Date(dateValue - 1000 * 60 * 5));
				simulateDeviceBatchTransferEnd();
			}
		}

		public function simulateDeviceOffsetFromNow(daysBeforeNow:Number, maximumRandomDaysFraction:Number):void
		{
			var correctedMeasuredDate:Date = new Date(_currentDateSource.now().valueOf() -
					DateUtil.MILLISECONDS_IN_DAY * daysBeforeNow -
					(DateUtil.MILLISECONDS_IN_DAY * maximumRandomDaysFraction * Math.random()));
			var bloodGlucose:int = Math.round(70 + Math.random() * 200);
			simulateDevice(bloodGlucose, correctedMeasuredDate);
		}

		private function addMenuItem(label:String, listener:Function):void
		{
			var menuItem:ViewMenuItem = new ViewMenuItem();
			menuItem.label = label;
			menuItem.addEventListener(MouseEvent.CLICK, listener);
			viewMenuItems.push(menuItem)
		}

		private function simulateDevice(bloodGlucose:int, correctedMeasuredDate:Date):void
		{
			var uriString:String = "collaborhythm://healthActionType=Equipment&healthActionName=Blood Glucose&equipmentName=FORA D40b&success=true&bloodGlucose=" +
					bloodGlucose + "&correctedMeasuredDate=" + DateUtil.format(correctedMeasuredDate, false) +
					"&deviceMeasuredDate=" + DateUtil.format(correctedMeasuredDate, false) + "&localTransmittedDate=2012-11-07T15:48:00-05:00&deviceTransmittedDate=2012-11-07T15:48:00-05:00";
			NativeApplication.nativeApplication.dispatchEvent(new InvokeEvent(InvokeEvent.INVOKE, false, false, null,
					[uriString]))
		}

		private function simulateDeviceBatchTransferBegin():void
		{
			simulateDeviceBatchTransfer("begin");
		}

		private function simulateDeviceBatchTransferEnd():void
		{
			simulateDeviceBatchTransfer("end");
		}

		private function simulateDeviceBatchTransfer(batchTransfer:String):void
		{
			NativeApplication.nativeApplication.dispatchEvent(new InvokeEvent(InvokeEvent.INVOKE, false, false, null,
					["collaborhythm://healthActionType=Equipment&healthActionName=Blood Glucose&equipmentName=FORA D40b&success=true&" + BloodGlucoseHealthActionInputController.BATCH_TRANSFER_URL_VARIABLE + "=" +
							batchTransfer]));
		}
		]]></fx:Script>
	<view:ReportBloodGlucoseDetailedView id="reportBloodGlucoseDetailedView" height="{footerGroup.y - reportBloodGlucoseDetailedView.y}"/>
	<s:Group id="footerGroup" width="100%" bottom="0">
		<s:Rect width="100%" height="100%" visible="{_dataInputModel.reportBloodGlucoseItemDataCollection.length > 1}">
			<s:fill>
				<s:SolidColor color="0xFFFFFF"/>
			</s:fill>
		</s:Rect>
		<s:VGroup paddingTop="10" paddingBottom="{_dataInputModel.reportBloodGlucoseItemDataCollection.length > 1 ? 10 : 90}" width="100%" horizontalAlign="center">
			<s:Button id="saveButton" label="Save"
					  enabled="false" fontSize="40" click="saveButton_clickHandler()"/>
		</s:VGroup>
	</s:Group>
</tablet:TabletViewBase>