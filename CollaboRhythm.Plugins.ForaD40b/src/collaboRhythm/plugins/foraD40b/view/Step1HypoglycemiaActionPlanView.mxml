<?xml version="1.0"?>
<!---
 - Copyright 2012 John Moore, Scott Gilroy
 -
 - This file is part of CollaboRhythm.
 -
 - CollaboRhythm is free software: you can redistribute it and/or modify it under the terms of the GNU General Public
 - License as published by the Free Software Foundation, either version 2 of the License, or (at your option) any later
 - version.
 -
 - CollaboRhythm is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied
 - warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 - details.
 -
 - You should have received a copy of the GNU General Public License along with CollaboRhythm.  If not, see
 - <http://www.gnu.org/licenses/>.
-->
<view:HypoglycemiaActionPlanViewBase xmlns:fx="http://ns.adobe.com/mxml/2009"
									 xmlns:s="library://ns.adobe.com/flex/spark"
									 xmlns:view="collaboRhythm.plugins.foraD40b.view.*"
									 creationComplete="creationCompleteHandler(event)"
									 implements="collaboRhythm.plugins.schedule.shared.model.IHealthActionInputView"
									 title="Hypoglycemia Action Plan - Step 1: Eat Simple Carbs">
	<fx:Style>
		#instructionsLabel {
			fontSize: 36;
		}
	</fx:Style>
	<fx:Script><![CDATA[
		import collaboRhythm.plugins.foraD40b.model.BloodGlucoseHealthActionInputModel;
		import collaboRhythm.plugins.foraD40b.model.SimpleCarbsItem;
		import collaboRhythm.shared.collaboration.model.CollaborationLobbyNetConnectionServiceProxy;

		import mx.binding.utils.BindingUtils;
		import mx.collections.ArrayCollection;
		import mx.events.FlexEvent;

		[Embed(source="/assets/images/juice.png")]
		public var juiceImageClass:Class;

		[Embed(source="/assets/images/soda.png")]
		public var sodaImageClass:Class;

		[Embed(source="/assets/images/glucoseTablets.png")]
		public var glucoseTabletsImageClass:Class;

		[Embed(source="/assets/images/honey.png")]
		public var honeyImageClass:Class;

		public static const DRINK_1_2_CUP_OF_FRUIT_JUICE:String = "Drink 1/2 cup of fruit juice";
		public static const DRINK_1_2_CUP_OF__NON_DIET__SODA:String = "Drink 1/2 cup of (non-diet) soda";
		public static const TAKE_4_GLUCOSE_TABLETS:String = "Take 4 glucose tablets";
		public static const EAT_15G_OF_OTHER_SIMPLE_CARBS:String = "Eat 15g of other simple carbs";
		public static const DRINK_1_CUP_OF_FRUIT_JUICE:String = "Drink 1 cup of fruit juice";
		public static const DRINK_1_CUP_OF__NON_DIET__SODA:String = "Drink 1 cup of (non-diet) soda";
		public static const TAKE_8_GLUCOSE_TABLETS:String = "Take 8 glucose tablets";
		public static const EAT_30G_OF_OTHER_SIMPLE_CARBS:String = "Eat 30g of other simple carbs";

		private function creationCompleteHandler(event:FlexEvent):void
		{
			hypoglycemiaActionPlanView.init(_dataInputModel);

			var imageClassArray:Array = [juiceImageClass, sodaImageClass, glucoseTabletsImageClass, honeyImageClass];
			var textArray:Array;
			var hypoglycemiaTextArray:Array = [DRINK_1_2_CUP_OF_FRUIT_JUICE, DRINK_1_2_CUP_OF__NON_DIET__SODA, TAKE_4_GLUCOSE_TABLETS, EAT_15G_OF_OTHER_SIMPLE_CARBS];
			var severeHypoglycemiaTextArray:Array = [DRINK_1_CUP_OF_FRUIT_JUICE, DRINK_1_CUP_OF__NON_DIET__SODA, TAKE_8_GLUCOSE_TABLETS, EAT_30G_OF_OTHER_SIMPLE_CARBS];
			var quickSugarItemArrayCollection:ArrayCollection = new ArrayCollection();

			if (_dataInputModel.glycemicState == BloodGlucoseHealthActionInputModel.HYPOGLYCEMIA)
			{
				textArray = hypoglycemiaTextArray;
			}
			else if (_dataInputModel.glycemicState == BloodGlucoseHealthActionInputModel.SEVERE_HYPOGLYCEMIA)
			{
				textArray = severeHypoglycemiaTextArray;
			}

			var index:int = 0;
			for each (var imageClass:Class in imageClassArray)
			{
				var quickSugarItemText:String = textArray[index];
				var quickSugarItem:SimpleCarbsItem = new SimpleCarbsItem(imageClass, quickSugarItemText);
				quickSugarItemArrayCollection.addItem(quickSugarItem);
				index++;
			}

			simpleCarbsItemList.dataProvider = quickSugarItemArrayCollection;

			if (_dataInputModel.healthActionModelDetailsProvider.accountId !=
					_dataInputModel.healthActionModelDetailsProvider.activeAccount.accountId)
			{
				simpleCarbsItemList.enabled = false;
			}

			BindingUtils.bindSetter(simpleCarbsItemListSelectItem, _dataInputModel, "simpleCarbsItemListSelectedIndex");
		}

		private function simpleCarbsItemListSelectItem(selecedIndex:int):void
		{
			simpleCarbsItemList.selectedIndex = selecedIndex;
		}

		private function simpleCarbsItemList_changeHandler():void
		{
			_dataInputController.simpleCarbsItemList_changeHandler(simpleCarbsItemList.selectedIndex, true);

			if (simpleCarbsItemList.selectedItem != null &&
					_dataInputModel.healthActionModelDetailsProvider.accountId ==
							_dataInputModel.healthActionModelDetailsProvider.activeAccount.accountId)
			{
				nextStepButton.enabled = true;
				nextStepActionButton.enabled = true;
			}
		}

		override protected function nextStepButton_clickHandler():void
		{
			_dataInputController.addEatCarbsHealthAction((simpleCarbsItemList.selectedItem as SimpleCarbsItem).text,
					true);

			super.nextStepButton_clickHandler();
		}
		]]></fx:Script>
	<s:Group width="100%" height="100%">
		<view:HypoglycemiaActionPlanStepsView id="hypoglycemiaActionPlanView"/>
		<s:Group width="590" height="360" top="160" horizontalCenter="0" verticalCenter="0">
			<s:List id="simpleCarbsItemList" width="100%" height="100%"
					itemRenderer="collaboRhythm.plugins.foraD40b.view.SimpleCarbsItemRenderer"
					verticalScrollPolicy="off" horizontalScrollPolicy="off"
					change="simpleCarbsItemList_changeHandler()"/>
			<s:Rect width="100%" height="100%">
				<s:stroke>
					<s:SolidColorStroke color="0x808285"/>
				</s:stroke>
			</s:Rect>
		</s:Group>
		<s:Button id="nextStepButton" label="Next Step" width="160" height="50" bottom="90" horizontalCenter="0"
				  enabled="false" click="nextStepButton_clickHandler()"/>
	</s:Group>
</view:HypoglycemiaActionPlanViewBase>
